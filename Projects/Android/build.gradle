apply plugin: 'com.android.application'
apply from: "${rootProject.projectDir}/XrApp.gradle"

android {
  // This is the name of the generated apk file, which will have
  // -debug.apk or -release.apk appended to it.
  // The filename doesn't effect the Android installation process.
  // Use only letters to remain compatible with the package name.
  project.archivesBaseName = "jkxr"

  defaultConfig {
    // Gradle replaces the manifest package with this value, which must
    // be unique on a system.  If you don't change it, a new app
    // will replace an older one.
    applicationId "com.drbeef." + project.archivesBaseName

    // override app plugin abiFilters for both 32 and 64-bit support
      externalNativeBuild {
          ndk {
              abiFilters 'arm64-v8a'
          }
          ndkBuild {
              abiFilters 'arm64-v8a'
          }
      }

      minSdkVersion 29
      targetSdkVersion 29
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['../../java']
      jniLibs.srcDir 'libs'
      res.srcDirs = ['../../res']
      assets.srcDirs = ['../../assets']
    }
  }

    packagingOptions {
        exclude 'lib/arm64-v8a/libopenxr_loader.so'
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    compileSdkVersion = 29
    buildToolsVersion = '29.0.3'
}

dependencies {
    implementation "com.android.support:support-compat:28.0.0"
    implementation "com.android.support:support-core-utils:28.0.0"
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
}

repositories {
    google()
}
buildscript {
    repositories {
        google()
    }
}

task packBaseResources(type: Zip) {
    from "../../z_vr_assets_base/"
    destinationDir file("../../assets/")
    archiveName "z_vr_assets_base.pk3"
}

task packJOResources(type: Zip) {
    from "../../z_vr_assets_jko/"
    destinationDir file("../../assets/")
    archiveName "z_vr_assets_jko.pk3"
}

tasks.matching {it.name.startsWith("assemble")}.all { Task task ->
    task.dependsOn([packBaseResources, packJOResources])
}
